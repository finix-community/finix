{
  config,
  pkgs,
  lib,
  ...
}:
let
  cfg = config.services.sysklogd;
in
{
  options.services.sysklogd = {
    enable = lib.mkOption {
      type = lib.types.bool;
      default = false;
      description = ''
        Whether to enable [sysklogd](${pkgs.sysklogd.meta.homepage}) as a system service.
      '';
    };

    package = lib.mkOption {
      type = lib.types.package;
      default = pkgs.sysklogd;
      defaultText = lib.literalExpression "pkgs.sysklogd";
      description = ''
        The package to use for `sysklogd`.
      '';
    };
  };

  # finit has explicit sysklogd support, requires `logger` to be available in `PATH`
  options.finit = lib.optionalAttrs cfg.enable {
    services = lib.mkOption {
      type = lib.types.attrsOf (
        lib.types.submodule (
          { config, ... }:
          {
            config.path = lib.optionals config.log [ cfg.package ];
          }
        )
      );
    };

    tasks = lib.mkOption {
      type = lib.types.attrsOf (
        lib.types.submodule (
          { config, ... }:
          {
            config.path = lib.optionals config.log [ cfg.package ];
          }
        )
      );
    };
  };

  config = lib.mkIf cfg.enable {
    # finit has explicit sysklogd support, requires `logger` to be available in `PATH`
    finit.path = [
      cfg.package
    ];

    finit.services.syslogd = {
      description = "system logging daemon";
      runlevels = "S0123456789";
      conditions =
        lib.optionals config.services.udev.enable [ "run/udevadm:5/success" ]
        ++ lib.optionals config.services.mdevd.enable [ "run/coldplug/success" ];
      command = "${cfg.package}/bin/syslogd -F";
      notify = "pid";
    };

    environment.etc."syslog.conf".source = "${cfg.package}/share/doc/sysklogd/syslog.conf";
  };
}
